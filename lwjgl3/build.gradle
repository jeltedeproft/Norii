buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "org.beryx:badass-runtime-plugin:1.8.5"
	}
}

apply plugin: 'application'
apply plugin: 'org.beryx.runtime'

sourceSets.main.resources.srcDirs += [ rootProject.file('assets').path ]
mainClassName = 'com.jelte.norii.lwjgl3.Lwjgl3Launcher'
eclipse.project.name = appName + '-lwjgl3'
sourceCompatibility = 11.0

dependencies {
	implementation project(':core')
	implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
	implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
	api ("com.badlogicgames.gdx:gdx-tools:$gdxVersion") {
       exclude group: 'com.badlogicgames.gdx', module: 'gdx-backend-lwjgl'
    }
	implementation "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
	implementation "com.badlogicgames.gdx:gdx-controllers-lwjgl3:$gdxVersion"
	implementation "com.badlogicgames.gdx-video:gdx-video-lwjgl3:$gdxVideoVersion"
	api "com.github.MrStahlfelge.gdx-websockets:common:$wsVersion"
	api "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
}

import org.gradle.internal.os.OperatingSystem

run {
	workingDir = rootProject.file('assets').path
	setIgnoreExitValue(true)
	
	if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
		// Required to run LWJGL3 Java apps on MacOS
		jvmArgs += "-XstartOnFirstThread"
	}
}
jar {
	destinationDirectory = file("$buildDir/lib")
	archiveBaseName = appName
	dependsOn configurations.runtimeClasspath
	from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } } 
	manifest {
		attributes 'Main-Class': project.mainClassName
	}
	doLast {
		file(archivePath).setExecutable(true, false)
	}
}

jpackageImage.dependsOn jar

runtime {
	options = ['--strip-debug',
			   '--compress', '2',
			   '--no-header-files',
			   '--no-man-pages',
			   '--strip-native-commands',
			   '--vm', 'server']
	modules = ['java.base' ,
			   'java.desktop',
			   'jdk.unsupported']

	jpackage {
		installerOptions += [ '--vendor', 'jeltedeproft.dev', '--description', 'Turn based strategy game', '--app-version', '0.0.2' ]
		mainJar = jar.archiveFileName.get()
		installerName = imageName = appName

		if (OperatingSystem.current().windows) {
			imageOptions += [ '--win-console'] //, "--icon", file("../assets/icons/Icon.ico") ]
			installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut', '--win-upgrade-uuid', '384b3846-8585-45d1-a1e3-5e5145019482']
		} else {
			//imageOptions += ["--icon", file("../assets/icons/Icon-32.png")]
			installerOptions += ['--linux-menu-group', '--linux-shortcut']
		}
	}
}

